/*
 * Copyright (c) 2017. tangzx(love.tangzx@qq.com)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

{
  parserClass="com.tang.intellij.lua.parser.LuaParser"

  implements="com.tang.intellij.lua.psi.LuaPsiElement"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Lua"
  psiImplClassSuffix="Impl"
  psiPackage="com.tang.intellij.lua.psi"
  psiImplPackage="com.tang.intellij.lua.psi.impl"
  psiImplUtilClass="com.tang.intellij.lua.psi.LuaPsiImplUtilKt"

  parserUtilClass="com.tang.intellij.lua.psi.LuaParserUtil"
  elementTypeHolderClass="com.tang.intellij.lua.psi.LuaTypes"
  elementTypeClass="com.tang.intellij.lua.psi.LuaElementType"
  elementTypeFactory="com.tang.intellij.lua.lang.LuaParserDefinitionKt.createType"
  tokenTypeClass="com.tang.intellij.lua.psi.LuaTokenType"
  tokenTypeFactory="com.tang.intellij.lua.lang.LuaParserDefinitionKt.createToken"
  parserImports = "static com.intellij.lang.WhitespacesBinders.*"

  tokens = [
        /* Keywords */
        AND = "and"
        BREAK = "break"
        DO = "do"
        ELSE = "else"
        ELSEIF = "elseif"
        END = "end"
        FALSE = "false"
        FOR = "for"
        FUNCTION = "function"
        IF = "if"
        IN = "in"
        LOCAL = "local"
        NIL = "nil"
        NOT = "not"
        OR = "or"
        REPEAT = "repeat"
        RETURN = "return"
        THEN = "then"
        TRUE = "true"
        UNTIL = "until"
        WHILE = "while"
        REGION = "REGION"
        ENDREGION = "ENDREGION"

        SHEBANG = "#!"
        SHEBANG_CONTENT = "SHEBANG_CONTENT"
        ELLIPSIS = "..."
        CONCAT = ".."
        EQ = "=="
        GE = ">="
        LE = "<="
        NE = "~="
        MINUS = "-"
        PLUS = "+"
        MULT = "*"
        MOD = "%"
        DIV = "/"
        ASSIGN = "="
        GT = ">"
        LT = "<"
        LPAREN = "("
        RPAREN = ")"
        LBRACK = "["
        RBRACK = "]"
        LCURLY = "{"
        RCURLY = "}"
        GETN = "#"
        COMMA = ","
        SEMI = ";"
        COLON = ":"
        DOT = "."
        EXP = "^"

        SHORT_COMMENT="SHORT_COMMENT"
        DOC_BLOCK_COMMENT="DOC_BLOCK_COMMENT"
        DOC_COMMENT="DOC_COMMENT"
        BLOCK_COMMENT="BLOCK_COMMENT"
        ID="regexp:[A-Za-z_][A-Za-z0-9_]*"
        NUMBER="NUMBER"
        STRING = "STRING"

        //lua 5.3
        DOUBLE_COLON = "::"
        GOTO = "goto"
        BIT_OR = "|"
        BIT_AND = "&"
        BIT_LTLT = "<<"
        BIT_RTRT = ">>"
        BIT_TILDE = "~"
        DOUBLE_DIV = "//"
  ]

  hooks("localDefStat|assignStat|tableField") = [leftBinder="MY_LEFT_COMMENT_BINDER" rightBinder="MY_RIGHT_COMMENT_BINDER"]
  hooks("fieldList") = [leftBinder="GREEDY_LEFT_BINDER" rightBinder="GREEDY_RIGHT_BINDER"]
  implements(".*Attribute") = "com.tang.intellij.lua.psi.LuaAttribute"
  implements(".*Stat") = "com.tang.intellij.lua.psi.LuaStatement"
  hooks(".*Expr") = [leftBinder="MY_LEFT_COMMENT_BINDER"]
  hooks("exprList") = [leftBinder="MY_LEFT_COMMENT_BINDER"]
  hooks(".*Stat") = [leftBinder="MY_LEFT_COMMENT_BINDER"]
}

luaFile ::= shebang_line? stat_semi*

shebang_line ::= SHEBANG SHEBANG_CONTENT

private stat_semi ::= <<parseStatement>>
block ::= stat_semi* {
    implements = [
        "com.tang.intellij.lua.psi.LuaDeclarationScope"
    ]
}

private stat_impl ::=
    emptyStat |
    doStat |
    whileStat |
    repeatStat |
    ifStat |
    forAStat |
    forBStat |
    defStat |
    lastStat |
    labelStat |
    gotoStat |
    assignStat |
    exprStat {
        recoverWhile = stat_recover
    }
private stat_recover ::= !(ID
    | ',' | ';'
    | 'local' | 'do' | 'while' | 'repeat' | 'function' | 'if' | 'for' | 'return' | break
    | nil | true | false | STRING | NUMBER | '::' | 'goto'
    | unaryOp)

emptyStat ::= ';'

varList ::= varExpr (',' varExpr)* {
    extends = exprList
    stubClass = "com.tang.intellij.lua.stubs.LuaPlaceholderStub"
}
assignStat ::= varList '=' exprList {
    pin = 2
    implements = [
        "com.tang.intellij.lua.psi.LuaStatement"
        "com.tang.intellij.lua.psi.LuaDeclaration"
    ]
    methods = [assign = 'ASSIGN' varExprList = '/exprList[0]' valueExprList = '/exprList[1]']
    stubClass = "com.tang.intellij.lua.stubs.LuaPlaceholderStub"
}
doStat ::= 'do' <<lazyBlock>> 'end' {
    pin = 1
    implements = [
        "com.tang.intellij.lua.psi.LuaStatement"
        "com.tang.intellij.lua.psi.LuaIndentRange"
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaPlaceholderStub"
}
whileStat ::= 'while' expr 'do' <<lazyBlock>> 'end' {
    pin = 1
    implements = [
        "com.tang.intellij.lua.psi.LuaStatement"
        "com.tang.intellij.lua.psi.LuaLoop"
        "com.tang.intellij.lua.psi.LuaIndentRange"
    ]
}
repeatStat ::= 'repeat' <<lazyBlock>> 'until' expr {
    pin = 1
    implements = [
        "com.tang.intellij.lua.psi.LuaStatement"
        "com.tang.intellij.lua.psi.LuaLoop"
        "com.tang.intellij.lua.psi.LuaIndentRange"
        "com.tang.intellij.lua.psi.LuaDeclarationScope"
    ]
    methods = [
        until = 'UNTIL'
    ]
}
ifStat ::= 'if' expr 'then' <<lazyBlock>> ('elseif' expr 'then' <<lazyBlock>>)* ('else' <<lazyBlock>>)? 'end' {
    pin = 1
    implements = [
        "com.tang.intellij.lua.psi.LuaStatement"
        "com.tang.intellij.lua.psi.LuaIndentRange"
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaPlaceholderStub"
}
forAStat ::= 'for' paramDef '=' expr ',' expr (',' expr)? 'do' <<lazyBlock>> 'end' {
    pin = 3
    methods = [getParamDefList]
    implements = [
        "com.tang.intellij.lua.psi.LuaStatement"
        "com.tang.intellij.lua.psi.LuaParametersOwner<com.tang.intellij.lua.stubs.LuaPlaceholderStub>"
        "com.tang.intellij.lua.psi.LuaLoop"
        "com.tang.intellij.lua.psi.LuaIndentRange"
        "com.tang.intellij.lua.psi.LuaDeclarationScope"
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaPlaceholderStub"
}
forBStat ::= 'for' parList 'in' exprList 'do' <<lazyBlock>> 'end' {
    pin = 1
    implements = [
        "com.tang.intellij.lua.psi.LuaStatement"
        "com.tang.intellij.lua.psi.LuaParametersOwner<com.tang.intellij.lua.stubs.LuaPlaceholderStub>"
        "com.tang.intellij.lua.psi.LuaLoop"
        "com.tang.intellij.lua.psi.LuaIndentRange"
        "com.tang.intellij.lua.psi.LuaDeclarationScope"
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaPlaceholderStub"
}
labelStat ::= '::' ID '::' {
    pin = 1
    implements = [
        "com.tang.intellij.lua.psi.LuaStatement"
        "com.intellij.psi.PsiNameIdentifierOwner"
    ]
    methods = [
        getNameIdentifier
        getTextOffset
        setName
        getName
    ]
}
gotoStat ::= 'goto' ID {
    pin = 1
    methods = [
        getReferences
    ]
}
private defStat ::= classMethodDefStat | funcDefStat | localFuncDefStat | localDefStat
exprStat ::= expr {
    stubClass = "com.tang.intellij.lua.stubs.LuaPlaceholderStub"
    methods = [
        getExpression
    ]
}

funcDefStat ::= 'function' ID funcBody {
    pin = 1
    implements = [
        "com.tang.intellij.lua.psi.LuaTypeMethod<com.tang.intellij.lua.stubs.LuaFuncDefStatStub>"
        "com.tang.intellij.lua.psi.LuaDeclaration"
        "com.tang.intellij.lua.psi.LuaTypeScope"
    ]
    methods = [
        getPresentation
        getParamDefList
        getNameIdentifier
        setName
        getName
        getTextOffset
        toString
        guessReturnType
        guessParentType
        getVisibility
        isDeprecated
        isExplicitlyTyped
        getParams
        getReferences
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaFuncDefStatStub"
}

private checkFuncPrefix ::= ID ('.' | ':') ID

left funcPrefixRef ::= '.' <<repeat checkFuncPrefix 1>> ID { extends = indexExpr elementType = indexExpr }
classMethodName ::=  nameExpr (funcPrefixRef)* (('.' ID) | (':' ID)) {
    pin = 1
    methods = [
        getExpression
        dot = 'DOT'
        colon = 'COLON'
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaPlaceholderStub"
}
classMethodDefStat ::= 'function' classMethodName funcBody {
    pin = 2
    implements = [
        "com.tang.intellij.lua.psi.LuaTypeMethod<com.tang.intellij.lua.stubs.LuaClassMethodDefStatStub>"
        "com.tang.intellij.lua.psi.LuaDeclaration"
        "com.tang.intellij.lua.psi.LuaTypeScope"
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaClassMethodDefStatStub"
    methods = [
        guessParentType
        getVisibility
        isDeprecated
        isExplicitlyTyped
        getParamDefList
        getNameIdentifier
        setName
        getName
        getTextOffset
        toString
        guessReturnType
        getParams
        isStatic
        getPresentation
    ]
}

localFuncDefStat ::= 'local' 'function' ID funcBody {
    pin = 2
    implements = [
        "com.tang.intellij.lua.psi.LuaFuncBodyOwner<com.tang.intellij.lua.stubs.LuaLocalFuncDefStub>"
        "com.tang.intellij.lua.psi.LuaDeclaration"
        "com.tang.intellij.lua.psi.LuaTypeScope"
        "com.intellij.psi.PsiNameIdentifierOwner"
    ]
    methods = [
        getParamDefList
        getNameIdentifier
        setName
        getName
        getTextOffset
        getUseScope
        guessReturnType
        guessParentType
        getParams
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaLocalFuncDefStub"
}
closeAttribute ::= 'close'
constAttribute ::= 'const'
private attribute ::= '<' (closeAttribute | constAttribute) '>'
localDef ::= ID attribute? {
    implements = [
        "com.tang.intellij.lua.psi.LuaNamedElement"
        "com.tang.intellij.lua.psi.LuaPsiTypeGuessable"
        "com.intellij.psi.PsiNameIdentifierOwner"
    ]
    methods = [
        getName
        setName
        getNameIdentifier
        getUseScope
        close = 'closeAttribute'
        const = 'constAttribute'
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaLocalDefStub"
}
private localDefList ::= localDef (',' localDef)*
localDefStat ::= 'local' localDefList ('=' exprList)? {
    pin = 1
    stubClass = "com.tang.intellij.lua.stubs.LuaPlaceholderStub"
    implements = [
        "com.tang.intellij.lua.psi.LuaDeclaration"
        "com.tang.intellij.lua.psi.LuaDeclarationScope"
    ]
    methods = [assign = 'ASSIGN']
}

returnStat ::= return exprList? {
	stubClass = "com.tang.intellij.lua.stubs.LuaReturnStatStub"
    implements = [
        "com.tang.intellij.lua.psi.LuaStatement"
        "com.tang.intellij.lua.psi.LuaDeclaration"
    ]
    methods = [
        getType
    ]
}
breakStat ::= break
private lastStat ::= returnStat | breakStat

exprList ::= expr (',' expr)* {
    methods = [
        guessTypeAt
        getExpressionList
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaPlaceholderStub"
}

literalExpr ::= nil | false | true | NUMBER | STRING | "..." {
    mixin = "com.tang.intellij.lua.psi.impl.LuaLiteralExprMixin"
    implements = [
        "com.tang.intellij.lua.psi.LuaExpression<com.tang.intellij.lua.stubs.LuaLiteralExprStub>"
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaLiteralExprStub"
}
stringExpr ::= STRING {
    extends = literalExpr
    elementType = literalExpr
}

private varExpr ::= primaryExpr

private expr ::= <<parseExpr>> {
    implements = [
        "com.tang.intellij.lua.psi.LuaExpression"
    ]
}

parenExpr ::= '(' expr ')' {
    pin = 1
    mixin = "com.tang.intellij.lua.psi.impl.LuaExprMixin<com.tang.intellij.lua.stubs.LuaExprPlaceStub>"
    implements = [
        "com.tang.intellij.lua.psi.LuaExpression<com.tang.intellij.lua.stubs.LuaExprPlaceStub>"
    ]
    methods = [
        getExpression
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaExprPlaceStub"
}
unaryExpr ::= unaryOp (unaryExpr | primaryExpr) {
    pin = 1
    mixin = "com.tang.intellij.lua.psi.impl.LuaExprMixin<com.tang.intellij.lua.stubs.LuaUnaryExprStub>"
    implements = [
        "com.tang.intellij.lua.psi.LuaExpression<com.tang.intellij.lua.stubs.LuaUnaryExprStub>"
    ]
    methods = [
        getExpression
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaUnaryExprStub"
}

left binaryExpr ::= binaryOp expr {
    pin = 1
    mixin = "com.tang.intellij.lua.psi.impl.LuaExprMixin<com.tang.intellij.lua.stubs.LuaBinaryExprStub>"
    implements = [
        "com.tang.intellij.lua.psi.LuaExpression<com.tang.intellij.lua.stubs.LuaBinaryExprStub>"
    ]
    methods = [
        getOperationType
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaBinaryExprStub"
}

left indexExpr ::= '[' expr ']' | '.' ID | ':' ID {
    pin(".*") = 1
    methods = [
        getNameIdentifier
        setName
        getName
        getTextOffset
        getPresentation
        getIdExpr
        toString

        guessIndexType
        guessParentType
        isDeprecated
        isExplicitlyTyped

        getExpressionList

        dot = 'DOT'
        colon = 'COLON'
        lbrack = 'LBRACK'
    ]
    mixin = "com.tang.intellij.lua.psi.impl.LuaIndexExprMixin"
    implements = [
        "com.tang.intellij.lua.psi.LuaPsiTypeMember"
        "com.tang.intellij.lua.psi.LuaExpression<com.tang.intellij.lua.stubs.LuaIndexExprStub>"
        "com.intellij.psi.PsiNameIdentifierOwner"
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaIndexExprStub"
}

args ::= listArgs | singleArg {
    stubClass = "com.tang.intellij.lua.stubs.LuaPlaceholderStub"
}
singleArg ::= tableExpr | stringExpr {
	extends = args
	methods = [
		getExpression
	]
}
private arg_expr_list ::= (expr ',')* (expr |& ')') { pin = 1 }
listArgs ::= '(' (arg_expr_list)? ')' {
	extends = args pin = 1
	methods = [
		getExpressionList
	]
}

left callExpr ::= args {
    methods = [
        guessParentType
        getFirstStringArg

        isMethodDotCall
        isMethodColonCall
        isFunctionCall

        getExpression
    ]
    mixin = "com.tang.intellij.lua.psi.impl.LuaExprMixin<com.tang.intellij.lua.stubs.LuaExprPlaceStub>"
    implements = [
        "com.tang.intellij.lua.psi.LuaExpression<com.tang.intellij.lua.stubs.LuaExprPlaceStub>"
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaExprPlaceStub"
}

private suffixExpr ::= indexExpr | callExpr
private prefixExpr ::= parenExpr | nameExpr | tableExpr | literalExpr
// prefixExpr | callExpr | indexExpr
private primaryExpr ::= prefixExpr (suffixExpr*)

closureExpr ::= 'function' funcBody {
    pin = 2
    mixin = "com.tang.intellij.lua.psi.impl.LuaExprMixin<com.tang.intellij.lua.stubs.LuaClosureExprStub>"
    implements = [
        "com.tang.intellij.lua.psi.LuaFuncBodyOwner<com.tang.intellij.lua.stubs.LuaClosureExprStub>"
        "com.tang.intellij.lua.psi.LuaExpression<com.tang.intellij.lua.stubs.LuaClosureExprStub>"
        "com.tang.intellij.lua.psi.LuaTypeScope"
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaClosureExprStub"
    methods = [
        getParamDefList
        guessReturnType
        guessParentType
        getParams
    ]
}

funcBody ::= '(' (parList)? ')' <<lazyBlock>>? 'end' {
    pin = 1
    implements = [
        "com.tang.intellij.lua.psi.LuaIndentRange"
        "com.tang.intellij.lua.psi.LuaDeclarationScope"
    ]
    methods = [
        RPAREN = 'RPAREN'
        ELLIPSIS = 'ELLIPSIS'
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaFuncBodyStub"
}

private parList ::= paramDef (',' paramDef)* (',' '...')? | '...' { recoverWhile = parList_recover }
private parList_recover ::= !(')'|'in')

tableExpr ::= '{' fieldList '}' {
    pin = 1
    methods = [
        findField
    ]
    mixin = "com.tang.intellij.lua.psi.impl.LuaExprMixin<com.tang.intellij.lua.stubs.LuaTableExprStub>"
    implements = [
        "com.tang.intellij.lua.psi.LuaIndentRange"
        "com.tang.intellij.lua.psi.LuaExpression<com.tang.intellij.lua.stubs.LuaTableExprStub>"
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaTableExprStub"
}

private fieldList ::= (tableField (tableFieldSep tableField)* (tableFieldSep)?)?

private tableField1 ::= '[' expr ']' '=' expr { pin = 1 }
private tableField2 ::= ID '=' expr { pin = 2 }

tableField ::= tableField1 | tableField2 | expr {
    methods = [
        getNameIdentifier
        setName
        getName
        getTextOffset

        toString

        getPresentation
        guessIndexType
        guessParentType
        getIndexType
        getVisibility
        isDeprecated
        isExplicitlyTyped
        getIdExpr
        getValueExpr

        getExpressionList

        lbrack = 'LBRACK'
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaTableFieldStub"
    implements = [
        "com.tang.intellij.lua.psi.LuaTypeField"
        "com.intellij.psi.PsiNameIdentifierOwner"
        "com.tang.intellij.lua.psi.LuaCommentOwner"
    ]
    recoverWhile = tableField_recover
}

private tableField_recover ::= !(tableFieldSep | '}' | '[')

tableFieldSep ::= ',' | ';'

binaryOp ::= '+' | '-' | '*' | '/' | '^' | '%' | '..' |
    '<' | '<=' | '>' | '>=' | '==' | '~=' |
    'and' | 'or'
    // lua5.3
    | '|' | '&' | '>>' | '<<' | '~' | '//'

unaryOp ::= '-' | 'not' | '#'
    // lua5.3
    | '~'

nameExpr ::= ID {
    mixin = "com.tang.intellij.lua.psi.impl.LuaNameExprMixin"
    implements = [
        "com.tang.intellij.lua.psi.LuaModuleField"
        "com.tang.intellij.lua.psi.LuaExpression<com.tang.intellij.lua.stubs.LuaNameExprStub>"
        "com.intellij.psi.PsiNameIdentifierOwner"
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaNameExprStub"
    methods = [
        setName
        getName
        getNameIdentifier
        getPresentation
        getReferences
        isDeprecated
        isExplicitlyTyped

        toString
    ]
}
paramDef ::= ID {
    implements = [
        "com.tang.intellij.lua.psi.LuaNamedElement"
        "com.tang.intellij.lua.psi.LuaPsiTypeGuessable"
        "com.intellij.psi.PsiNameIdentifierOwner"
    ]
    methods = [
        getName
        setName
        getNameIdentifier
        getUseScope
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaParamDefStub"
}
