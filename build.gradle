/*
 * Copyright (c) 2017. tangzx(love.tangzx@qq.com)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'org.jetbrains.intellij' version "0.6.5"
    id "org.jetbrains.kotlin.jvm" version "1.4.21"
}

def versions = [:]
versions["203"] = [
        ideaSDKVersion: "IC-203.5981.155",
        sinceBuild: "203",
        untilBuild: "203.*",
        archiveName: "IntelliJ-Luanalysis",
        targetCompatibilityLevel: 11
]
versions.each { shortVer, cfg ->
    task "build_${shortVer}"(type: GradleBuild) {
        tasks = ["buildPluginWithBunch", "test"]
        buildFile = 'build_plugin.gradle'
        startParameter.setProjectProperties([
            "IDEA_VER": shortVer
        ] + cfg)
    }
}

task buildAll(dependsOn: [build_203])

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile fileTree(dir: "libs", includes: ["*.jar"])
}

sourceCompatibility = 1.8
sourceSets {
    main {
        java.srcDirs 'gen'
        resources {
            exclude "debugger/**"
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs = ['-Xjvm-default=all', '-Xopt-in=kotlin.contracts.ExperimentalContracts']
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs = ['-Xjvm-default=all', '-Xopt-in=kotlin.contracts.ExperimentalContracts']
    }
}

intellij {
    type 'IC'
    updateSinceUntilBuild false
    downloadSources false
    version = versions["203"].ideaSDKVersion
    localPath System.getenv("IDEA_HOME_203")
}